\begin{lstlisting}[caption={Max diagram - Vertex area (Section: \ref{section:max-diagram})\label{appendix:max-diagram}}]
    #version 330 core

    in vec3 coords;
    in vec4 wedge_color[3]; // an array of 3 vectors of size 4 (since it is a triangle)
    out vec4 fragColor;

    void main()
    {
            if (coords[0] > coords[1]) {
                if (coords[0] > coords[2]) { // 0 > 1 && 0 > 2
                    fragColor = wedge_color[0];
                }
                else { // 0 > 1 && 2 > 0 --> 2 > 0 > 1
                    fragColor = wedge_color[2];
                }
            }
            else {
                if (coords[1] > coords[2]) { // 1 > 0 && 1 > 2
                    fragColor = wedge_color[1];
                }
                else { // 1 > 0 && 1 < 2 --> 2 > 1 > 0
                    fragColor = wedge_color[2];
                }
            }
    }
\end{lstlisting}

\vspace{10pt}

\begin{lstlisting}[caption={Min diagram - Edge area (Section: \ref{section:min-diagram})\label{appendix:min-diagram}}]
    #version 330 core
    in vec3 coords;
    in vec4 wedge_color[3]; // an array of 3 vectors of size 4 (since it is a triangle)
    out vec4 fragColor;

    void main()
    {
        if (coords[0] < coords[1]) {
            if (coords[0] < coords[2]) {
                fragColor = wedge_color[0];
            }
            else {
                fragColor = wedge_color[2];
            }
        }
        else {
            if (coords[1] < coords[2]) {
                fragColor = wedge_color[1];
            }
            else {
                fragColor = wedge_color[2];
            }
        }
    }
\end{lstlisting}
\vspace{10pt}

\begin{lstlisting}[caption={Edge structure} (Section \ref{section:edge-struct})]
    struct edge
    {
        float norm_edge;
        int index_v1;
        int index_v2;
        Point3d n1;
        Point3d n2;
        float value_mean_curvature;
        float cot_alpha;
        float cot_beta;
        float area_t1;
        float area_t2;
    };
\end{lstlisting} \label{Pseudocode:edge}

\vspace{10pt}

\begin{lstlisting}[caption={Region $\mathcal{A}_{Mixed}$ on an arbitrary mesh. \cite{meshlab} (Section: \ref{section:localaveraging})\label{appendix:localaveraging}}]
    A_Mixed = 0
    For each triangle T from the 1-ring neighborhood of x
    If T is non-obtuse (Voronoi safe)
        A_Mixed += Voronoi region of x in T
    Else
        If x is obtuse
            A_Mixed += area(T)/2
        Else
            A_Mixed += area(T)/4
    \end{lstlisting}

\vspace{10pt}

\begin{lstlisting}[caption={Vertex Shader for vertex/triangle flat shading and triangle Gouraud shading using lighting (Section: \ref{section:extend-flat-shading-lighting})\label{appendix:vs-flat-shading-lighting}}]
    #version 330 core
    layout (location = 0) in vec3 aPos;
    layout (location = 1) in vec3 aNormal;
    layout (location = 5) in vec3 aNormalTriangle;

    struct Light {
        vec3 position;

        vec3 ambient;
        vec3 diffuse;
        vec3 specular;
    };

    out vec4 color;

    uniform mat4 model;
    uniform mat4 view;
    uniform mat4 projection;

    uniform vec3 view_position;
    uniform Light light;
    uniform float shininess;

    uniform bool isFlat;


    // get specular color at current Pos
    vec3 get_specular(vec3 pos, vec3 normal, vec3 light_direction) {

        // get directional vector to the camera from pos
        vec3 view_direction = normalize(view_position - pos);

        // specular shading
        vec3 reflect_direction = - normalize(reflect(light_direction, normal));
        float specular_intensity = pow(max(dot(reflect_direction, view_direction), 0.0), shininess);

        // get resulting color
        return light.specular * specular_intensity;
    }


    // return diffuse at current Pos
    vec3 get_diffuse(float lambert_term) {
        return light.diffuse * lambert_term;
    }

    vec4 get_result_color_lighting(vec3 pos, vec3 normal, vec3 light_position) {
        vec3 light_direction = normalize(light_position - pos);
        float diffuse_intensity = max(dot(light_direction, normal), 0.0);

        vec3 ambient = light.ambient;
        vec3 diffuse = get_diffuse(diffuse_intensity);

        if(diffuse_intensity > 0.0001){
            vec3 specular = get_specular(pos, normal, light_direction);
            return vec4((ambient + diffuse + specular), 1.0);
        }

        return vec4((ambient + diffuse) , 1.0);
    }


    void main() {

        vec3 world_position = vec3(model * vec4(aPos, 1.0));
        vec3 world_normal;

        if(isFlat){ // triangle normal
            world_normal = mat3(transpose(inverse(model))) * aNormalTriangle;
        } else { // vertex normal
            world_normal = mat3(transpose(inverse(model))) * aNormal;
        }

        vec3 light_pos = vec3(projection * vec4(light.position, 1.0));

        color = get_result_color_lighting(world_position, world_normal, light_pos); // color obtained with lighting calculations

        gl_Position = projection * view * model * vec4(aPos, 1.0);
    }


\end{lstlisting}

\vspace{10pt}

\begin{lstlisting}[caption={Vertex Shader for constant/Gouraud Gaussian curvature and constant/Gouraud mean curvature (Sections: \ref{section:gc-curvature} and \ref{section:mc-curvature})}]
    #version 330 core
    layout (location = 0) in vec3 aPos;
    layout (location = 2) in vec3 gaussian_curvature;
    layout (location = 3) in vec3 mean_curvature_edge;
    layout (location = 4) in vec3 mean_curvature_vertex;

    out vec4 color;

    uniform mat4 model;
    uniform mat4 view;
    uniform mat4 projection;

    uniform float min_curvature;
    uniform float max_curvature;

    uniform bool isGaussian;
    uniform bool isMeanCurvatureEdge;

    vec3 interpolation(vec3 v0, vec3 v1, float t) {
        return (1 - t) * v0 + t * v1;
    }

    vec3 hsv2rgb(vec3 c)
    {
        vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
        vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
        return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
    }


    vec4 get_result_color(){
        float val = gaussian_curvature[0]; // gaussian_curvature is a vec3 composed by same value
        if(!isGaussian && !isMeanCurvatureEdge){
            val = mean_curvature_vertex[0]; // mean curvature is a vec3 composed by same value
        } else if(!isGaussian && isMeanCurvatureEdge){
            val = mean_curvature_edge[0]; // mean curvature is a vec3 composed by same value
        }

        // colors in HSV
       vec3 red = vec3(0.0, 1.0, 1.0); //h s v
       vec3 green = vec3(0.333, 1.0, 1.0);
        vec3 blue = vec3(0.6667, 1.0, 1.0);

        if (val < 0) { //negative numbers until 0
            return vec4(hsv2rgb(interpolation(green, red, min(val/min_curvature, 1.0))), 1.0);
        } else { //from 0 to positive
            return vec4(hsv2rgb(interpolation(green, blue, min(val/max_curvature, 1.0))), 1.0);
        }
    }


    void main() {
        vec3 pos = vec3(model * vec4(aPos, 1.0));

        color = get_result_color();

        gl_Position = projection * view * model * vec4(aPos, 1.0);
    }
\end{lstlisting}

\begin{lstlisting}[caption={Geometry Shader for triangle flat shading, vertex flat shading, constant gaussian curvature, constant mean curvature (Sections: \ref{section:section:vertex-area-chapter} and \ref{section:edge-area-chapter})}]
    #version 330 core

    layout (triangles) in;
    layout (triangle_strip, max_vertices = 3) out;

    in vec4 color[3]; // an array of 3 vectors of size 4 (since it is a triangle)
    out vec3 coords;
    out vec4 wedge_color[3]; // an array of 3 vectors of size 4 (since it is a triangle)

    void main()
    {
        wedge_color[0] = color[0];
        wedge_color[1] = color[1];
        wedge_color[2] = color[2];

        coords = vec3(1.0, 0.0, 0.0);
        gl_Position = gl_in[0].gl_Position;
        EmitVertex();

        coords = vec3(0.0, 1.0, 0.0);
        gl_Position = gl_in[1].gl_Position;
        EmitVertex();

        coords = vec3(0.0, 0.0, 1.0);
        gl_Position = gl_in[2].gl_Position;
        EmitVertex();

        EndPrimitive();
    }
\end{lstlisting}


